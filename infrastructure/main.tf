terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.0"
    }
    databricks = {
      source = "databricks/databricks"
    }
  }
}

module "vpc" {
  for_each = var.vpcs
  source   = "../modules/networking/vpc"

  project_name = var.project_name
  environment  = var.environment
  vpc_cidr     = each.value.cidr_block
}

module "subnets" {
  for_each = var.vpcs
  source   = "../modules/networking/subnets"

  project_name  = var.project_name
  environment   = var.environment
  vpc_id        = module.vpc[each.key].vpc_id
  subnet_config = each.value.subnets
}

module "gateways" {
  for_each = var.vpcs
  source   = "../modules/networking/gateways"

  project_name = var.project_name
  environment  = var.environment

  nat_gateway_config = {
    for k, v in each.value.subnets : k => module.subnets[each.key].subnet_ids[k] if v.public
  }
}

module "routing" {
  for_each = var.vpcs
  source   = "../modules/networking/routing"

  project_name      = var.project_name
  environment       = var.environment
  vpc_id            = module.vpc[each.key].vpc_id
  igw_id            = module.vpc[each.key].igw_id
  public_subnet_ids = module.subnets[each.key].public_subnet_ids

  private_routing_config = {
    for k, v in each.value.subnets : k => {
      subnet_id      = module.subnets[each.key].subnet_ids[k]
      nat_gateway_id = module.gateways[each.key].nat_gateway_ids[[for pk, pv in each.value.subnets : pk if pv.public && pv.availability_zone == v.availability_zone][0]]
    } if !v.public
  }
}

module "storage" {
  source = "../modules/storage"

  project_name = var.project_name
  environment  = var.environment
  s3_buckets   = var.s3_buckets
}

module "iam" {
  source = "../modules/iam"

  project_name        = var.project_name
  environment         = var.environment
  storage_bucket_arns = module.storage.bucket_arns
  iam_roles           = var.iam_roles
}



module "databricks" {
  source = "../modules/databricks"

  project_name        = var.project_name
  environment         = var.environment
  databricks_role_arn = module.iam.databricks_role_arn
  s3_buckets          = module.storage.bucket_names

  providers = {
    databricks = databricks
  }
}

/*
module "lambda" {
  source = "../modules/lambda"

  project_name    = var.project_name
  environment     = var.environment
  aws_region      = var.aws_region
  fred_api_key    = var.fred_api_key
  lambda_role_arn = module.iam.role_arns["fred-fetcher"]
  lambda_config   = var.lambda_config
}

module "bedrock" {
  source = "../modules/bedrock"

  project_name           = var.project_name
  environment            = var.environment
  aws_region             = var.aws_region
  kb_s3_bucket_arn       = module.storage.bucket_arns["kb-source"]
  kb_s3_bucket_name      = module.storage.bucket_names["kb-source"]
  lambda_function_arn    = module.lambda.function_arn
  bedrock_kb_role_arn    = module.iam.role_arns["bedrock-kb"]
  bedrock_agent_role_arn = module.iam.role_arns["bedrock-agent"]
  bedrock_config         = var.bedrock_config
}
*/
